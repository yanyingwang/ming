
#lang scribble/manual

@(require (for-label racket ming)
           scribble/eval
           scribble-rainbow-delimiters
           ming/scribble
           )
@(define the-eval
         (make-eval-factory '(ming/racket/base ming/racket/string)))

@script/rainbow-delimiters*

@margin-note{另可参见：@secref["zi--wen"]。}

@title[#:tag "string"]{诗}
“诗”（@tech[ #:doc '(lib "scribblings/reference/reference.scrbl") "String"]）是一种数据结构，也就是通常所说的“字符串”。名语言用单字表示之称为“诗”。 @; string 连

诗有“易诗”和“坚诗”之分：
@itemlist[
@item{“易诗”（“易”在这里是“易变的，可以被改变的”的意思）是一种内容可以被修改的字符串数据结构，通常很多诗相关的例程比如@racket[诗]、@racket[复诗]、@racket[诗段]等处理完原始数据之后的结果输出都是易诗；}
@item{“坚诗”与之相反，是一种内容固定不能被修改的字符串数据结构，通过半格双引号@racket[""]创建出来的诗是“坚诗”。要注意的是，所谓的“不能被修改”很多时候会不容易被注意到，这是因为很多诗相关的例程在处理“坚诗”的时候，会新生成“易诗”并作为结果替换输出之，间接的完成了“坚诗”到“易诗”的转化。真正操作“诗”的原始数据并加以修改的例程都会以@racket[!]结尾以表警示。}
]

@section[#:tag "string-base"]{基础例程}
@examples[#:eval (the-eval)
(诗 #\鹅 #\鹅 #\鹅 #\， #\曲 #\项 #\向 #\天 #\歌 #\。)
(复诗 5 #\鹅)
@; (名 鹅 "鹅鹅鹅，曲项向天歌。白毛浮绿水，绿掌拨清波。")
(诗长 "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。")
(诗提 "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。" 0)
(令 ([某诗 (诗->易诗 "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。")])
    (行示 某诗)
    (诗修! 某诗 1 #\白)
    (行示 某诗))
@; (诗改! (诗->易诗 "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。") 0 #\白)
(诗段 "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。" 10)
(诗段 "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。" 10 15)

(令 ([某A诗 (诗->易诗 "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。")]
     [某B诗 "大中小白黑蓝"])
    (行示 某A诗)
    (诗修!/以它段 某A诗 0 某B诗 2 4)
    (行示 某A诗))

(令 ([某诗 (诗->易诗 "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。")])
    (行示 某诗)
    (诗复! 某诗 #\呱)
    (行示 某诗))

(接诗 "鹅鹅鹅" "曲项向天歌" "白毛浮绿水" "红掌拨清波")
(诗->链 "鹅鹅鹅，曲项向天歌。")
(链->诗 (链 #\鹅 #\鹅 #\鹅 #\， #\曲 #\项 #\向 #\天 #\歌 #\。))
]

@defmapping[racket/base/string]


@section[#:tag "string-additional"]{额外例程}
@defmodule[ming/racket/string]

@examples[#:eval (the-eval)
(链合成诗 (链 "鹅" "鹅" "鹅" "，" "曲" "项" "向" "天" "歌" "。"))
(链合成诗 (链 "鹅" "鹅" "鹅" "，" "曲" "项" "向" "天" "歌" "。") "-")
(链合成诗 (链 "鹅" "鹅" "鹅" "，" "曲" "项" "向" "天" "歌" "。") "")

(诗碎成链 "鹅  鹅  鹅   \n \n 曲项向天歌 \n\r")
(诗碎成链 "鹅-鹅-鹅-曲-项-向-天-歌" "-")

(诗简空 "鹅  鹅    鹅   \n \n 曲项向天歌 \n\r")

(诗更 "鹅鹅鹅，曲项向天歌。" "鹅鹅鹅" "小黄鸭")
(诗更 "鹅鹅鹅，曲项向天歌。" "鹅鹅鹅" "")

(诗含? "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。" "白毛浮绿水")
(诗起? "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。" "鹅鹅鹅")
(诗终? "鹅鹅鹅，曲项向天歌。白毛浮绿水，红掌拨清波。" "拨清波。")
]

@defmapping[racket/string]